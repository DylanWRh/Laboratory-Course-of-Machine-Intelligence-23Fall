// linked message commands
integer CMD_SHOW_X = 1;
integer CMD_SHOW_O = 2;
integer CMD_SHOW_BLANK = 3;

// Whose turn is it? 0 for O, 1 for X
integer whoseTurn = 0; 

announceTurn()
{
    if (whoseTurn == 0)
    {
        llSay(0, "It's O's turn");
    }
    else 
    {
        llSay(0, "It's X's turn");
    }
}

// set a game tile to X by its link number
setLinkToX(integer linkNum)
{
    llMessageLinked(linkNum, CMD_SHOW_X, "", NULL_KEY);
}

// set a game tile to O by its link number
setLinkToO(integer linkNum)
{
    llMessageLinked(linkNum, CMD_SHOW_O, "", NULL_KEY);
}

// completely reset the game
resetGame()
{
    llMessageLinked(LINK_ALL_OTHERS, CMD_SHOW_BLANK, "", NULL_KEY);
    whoseTurn = 0;
    announceTurn();
}

default
{
    state_entry()
    {
        resetGame();
    }
        
    touch_start(integer total_number)
    {
        if (llDetectedLinkNumber(0) == llGetLinkNumber())
        {
            // user touched the controller prim, reset the game
            resetGame();
        }
        else
        {
            if (whoseTurn == 0)
            {
                setLinkToO(llDetectedLinkNumber(0));
            }
            else
            {
                setLinkToX(llDetectedLinkNumber(0));
            }
            whoseTurn = !whoseTurn;
            announceTurn();
        }
    }
}