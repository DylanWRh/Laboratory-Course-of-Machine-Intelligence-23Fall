// set the vehicle parameters
setupVehicle()
{
    llSetStatus(STATUS_PHYSICS, TRUE);
    llSetVehicleType(VEHICLE_TYPE_CAR);
    
    // The vehicle will get up to full speed in 1 second
    llSetVehicleFloatParam(VEHICLE_LINEAR_MOTOR_TIMESCALE, 1.0);
    
    // The motor will become ineffective after 3 seconds.
    llSetVehicleFloatParam(VEHICLE_LINEAR_MOTOR_DECAY_TIMESCALE, 3.0);
    
    // The angular motor will quickly get up to speed
    llSetVehicleFloatParam(VEHICLE_ANGULAR_MOTOR_TIMESCALE, 0.75);
        
    // Turn off angular and linear deflection 
    llSetVehicleFloatParam(VEHICLE_ANGULAR_DEFLECTION_EFFICIENCY, 0.0);
    llSetVehicleFloatParam(VEHICLE_LINEAR_DEFLECTION_EFFICIENCY, 0.0);

    // Set high linear friction in all directions except forward
    llSetVehicleVectorParam(VEHICLE_LINEAR_FRICTION_TIMESCALE, 
        <5000.0, 1.0, 1.0>);
    
    // Set very high angular friction around all axes except z
    llSetVehicleVectorParam(VEHICLE_ANGULAR_FRICTION_TIMESCALE, 
        <0.1, 0.1, 5000.0>); 
       
    // Turn on mouselook steering and decouple the camera
    llSetVehicleFlags(VEHICLE_FLAG_MOUSELOOK_STEER |
        VEHICLE_FLAG_CAMERA_DECOUPLED); 
    
    // Set the angular motor for mouselook steering, ignore 
    // y-axis rotation
    llSetVehicleVectorParam(VEHICLE_ANGULAR_MOTOR_DIRECTION, 
         <0.0, 0.0, 5.0>);
}

// stop this object from being a vehicle
noVehicle()
{
    // Turn off physics and vehicle params
    llSetVehicleType(VEHICLE_TYPE_NONE);
    llSetStatus(STATUS_PHYSICS, FALSE);
}

// give this prim a wedge shape and set it to about avatar size
setupObject()
{
    llSetPrimitiveParams([
    PRIM_TYPE, 
        PRIM_TYPE_BOX,
        PRIM_HOLE_DEFAULT,
        <0.75, 1.0, 0.0>,
        0.0,
        <0.0, 0.0, 0.0>,
        <1.0, 1.0, 0.0>,
        <0.0, 0.0, 0.0>,
    PRIM_SIZE,
        <4.0, 1.5, 0.5>
        ]);
}

// default state, not a vehicle
default
{
    state_entry()
    {
        noVehicle();
        setupObject();
                
        // Set the location avatars will sit
        llSitTarget(<-1.0, 0.0, 0.5>, ZERO_ROTATION);
    }
    
    changed(integer change)
    {
        if (change & CHANGED_LINK)
        {
            if (llAvatarOnSitTarget() != NULL_KEY)
            {
                // someone is sitting on the object; request permission
                // to take controls
                llRequestPermissions(
                    llAvatarOnSitTarget(),
                    PERMISSION_TAKE_CONTROLS); 
            }
        }
    }
    
    run_time_permissions(integer perm)
    {
        // the run time permissions have changed; if script can 
        // take controls, do that and go to the vehicle state
        if (perm & PERMISSION_TAKE_CONTROLS)
        {
            llTakeControls(
                CONTROL_FWD | CONTROL_BACK | 
                CONTROL_LEFT | CONTROL_RIGHT | 
                CONTROL_ROT_LEFT |CONTROL_ROT_RIGHT,
                TRUE, FALSE);
            state vehicle;
        }
    }
}

state vehicle
{
    state_entry()
    {
        setupVehicle();
    }
        
    changed(integer change)
    {
        // If the avatar sitting on this object gets up, stop 
        // being a vehicle
        if (change & CHANGED_LINK)
        {
            if (llAvatarOnSitTarget() == NULL_KEY)
            {
                // no one is sitting on the object; stop 
                // being a vehicle
                llReleaseControls();
                state default;
            }
        }
    }
    
    control(key from, integer level, integer edge)
    {
        integer pressed = (level & edge);
        vector velocity = ZERO_VECTOR;
        float speed = 15.0;
        
        vector turn = ZERO_VECTOR;
        float turnSpeed = PI;
        
        if (pressed & CONTROL_FWD)
        {
            velocity = <1.0, 0.0, 0.0>;
        }
        else if (pressed & CONTROL_BACK)
        {
            velocity = <-1.0, 0.0, 0.0>;
        } 
        else if (pressed & CONTROL_ROT_LEFT)
        {
            turn = <0.0, 0.0, 1.0>;
        }
        else if (pressed & CONTROL_ROT_RIGHT)
        {
            turn = <0.0, 0.0, -1.0>;
        }
        if (velocity != ZERO_VECTOR)
        {
            velocity = velocity * speed;
            llSetVehicleVectorParam(VEHICLE_LINEAR_MOTOR_DIRECTION,                       
                velocity);
        } 
        
        if (turn != ZERO_VECTOR)
        {
            turn = turn * turnSpeed;
            llSetVehicleVectorParam(VEHICLE_ANGULAR_MOTOR_DIRECTION,
                turn);
        }       
    }
}
