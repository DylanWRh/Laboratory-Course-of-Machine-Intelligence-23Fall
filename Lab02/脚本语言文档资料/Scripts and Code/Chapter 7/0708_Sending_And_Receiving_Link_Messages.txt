// if the script receives a link message with this as its integer
// part, it will send a link message to the next prim in this 
// link set
integer LM_CONTINUE = 1;  

// Send a message to the next prim in this linked object.
messageNextPrim()
{
    integer primToMessage = llGetLinkNumber() + 1;
    
    llMessageLinked(primToMessage, LM_CONTINUE, 
        "Hello from prim number " + (string)llGetLinkNumber(),
        NULL_KEY);
}

// send a message to every linked object
messageAllPrims()
{
    llMessageLinked(LINK_SET, 0, "All done", NULL_KEY);
}


default
{
    touch_start(integer numDetected)
    {
        // if we are the root prim
        if (llGetLinkNumber() == 1)
        {
            messageNextPrim();
        }
    }
    
    link_message(integer from, integer n, string s, key k)
    {
        llOwnerSay(
            "Prim number " + (string)llGetLinkNumber() +
            " got a message from prim number " + (string)from +
            ": \"" + s + "\"");
        
        if (n == LM_CONTINUE)
        {
            // if this is the last prim in the set, message everyone
            if (llGetLinkNumber() == llGetNumberOfPrims())
            {
                messageAllPrims();
            }
            else
            {
                messageNextPrim();
            }
        }
    }
}