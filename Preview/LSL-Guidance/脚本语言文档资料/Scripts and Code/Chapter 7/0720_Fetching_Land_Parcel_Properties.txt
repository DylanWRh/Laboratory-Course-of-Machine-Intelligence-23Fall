sayFlag(integer does, string feature)
{
    if (does)
    {
        llOwnerSay("Parcel does " + feature);
    }
    else
    {
        llOwnerSay("Parcel does not " + feature);
    }
}

string keyOrName(key id)
{
    string name = llKey2Name(id);
    if (name == "") 
    {   
        return (string)id;
    }
    return name;
}

getLandInfo(vector at)
{
    llOwnerSay("Details of parcel containing " + (string)at);

    // parcel details
    list details = llGetParcelDetails(at, 
        [PARCEL_DETAILS_NAME, PARCEL_DETAILS_DESC,
         PARCEL_DETAILS_OWNER, PARCEL_DETAILS_GROUP,
         PARCEL_DETAILS_AREA]);
    
    llOwnerSay("Parcel Name: " + llList2String(details, 0));
    llOwnerSay("Parcel Description: " + llList2String(details, 1));
    llOwnerSay("Parcel Owner: " + keyOrName(llList2Key(details, 2)));
    llOwnerSay("Parcel Group: " + keyOrName(llList2Key(details, 3)));
    llOwnerSay("Parcel Area: " + (string)llList2Integer(details, 4)
        +"sq meters");
    
    // parcel flags
    integer pFlags = llGetParcelFlags(at);
    sayFlag(pFlags & PARCEL_FLAG_ALLOW_FLY, "allow flying");
    sayFlag(pFlags & PARCEL_FLAG_ALLOW_SCRIPTS, 
      "allow outside scripts");
    sayFlag(pFlags & PARCEL_FLAG_ALLOW_LANDMARK, 
      "allow landmark creation");
    sayFlag(pFlags & PARCEL_FLAG_ALLOW_TERRAFORM, 
      "allow anyone to terraform");
    sayFlag(pFlags & PARCEL_FLAG_ALLOW_DAMAGE, "allow damage");
    sayFlag(pFlags & PARCEL_FLAG_ALLOW_CREATE_OBJECTS, 
      "allow anyone to create objects");
    sayFlag(pFlags & PARCEL_FLAG_USE_ACCESS_GROUP, 
      "restrict access to a specific group");
    sayFlag(pFlags & PARCEL_FLAG_USE_ACCESS_LIST, 
      "restrict access by an access list");
    sayFlag(pFlags & PARCEL_FLAG_USE_BAN_LIST, "use a ban list");
    sayFlag(pFlags & PARCEL_FLAG_USE_LAND_PASS_LIST, 
      "allow land passes to be purchased");
    sayFlag(pFlags & PARCEL_FLAG_LOCAL_SOUND_ONLY, 
      "restrict sounds to local sounds");
    sayFlag(pFlags & PARCEL_FLAG_RESTRICT_PUSHOBJECT, 
      "restrict the use of llPushObject");

    // prim counts
    llOwnerSay("Parcel currently has " + 
        (string)llGetParcelPrimCount(at, PARCEL_COUNT_TOTAL, FALSE) + 
        " out of a maximum of " + 
        (string)llGetParcelMaxPrims(at, FALSE) + "prims");
        
    // prim owners
    llOwnerSay("Prim owners:");
    list owners = llGetParcelPrimOwners(at);
    integer nOwners = llGetListLength(owners) / 2;
    integer x;
    for (x = 0; x < nOwners; x++)
    {
        llOwnerSay("  " + (string)llList2Integer(owners, x * 2 + 1) + 
            " prims owned by " +
            keyOrName(llList2Key(owners, x * 2)));
    }
}


default
{
    touch_start(integer total_number)
    {
        getLandInfo(llGetPos());
    }
}