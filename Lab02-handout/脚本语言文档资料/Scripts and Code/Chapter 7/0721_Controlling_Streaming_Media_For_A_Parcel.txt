integer dialogChannel = 99;
string pauseCommand = "pause";
string changeCommand = "change";
string playCommand = "play";

list channels = [
 "http://images.apple.com/movies/wb/oceans_13/oceans_13-tsr-h.ref.mov",
 "http://images.apple.com/movies/lionsgate/fido/fido-h.ref.mov",
 "http://images.apple.com/movies/independent/paris_je_taime/paris_je_taime-h.ref.mov"
    ];

integer currentChannel = 0;

// Change the streaming URL and start it playing in a loop
changeChannel()
{
    currentChannel = (currentChannel + 1) % llGetListLength(channels);
    llParcelMediaCommandList([
        PARCEL_MEDIA_COMMAND_URL, 
          llList2String(channels, currentChannel),
        PARCEL_MEDIA_COMMAND_LOOP
        ]);
}

// pause stream
pause()
{
    llParcelMediaCommandList([
        PARCEL_MEDIA_COMMAND_PAUSE
        ]);
}

play()
{
    llParcelMediaCommandList([
        PARCEL_MEDIA_COMMAND_LOOP
        ]);
}

default
{ 
    state_entry()
    {
        // Get the parcel media texture and apply it;
        list media = llParcelMediaQuery([PARCEL_MEDIA_COMMAND_TEXTURE]);
        key mediaTexture = llList2Key(media, 0);
        if (mediaTexture != NULL_KEY)
        {
            llSetTexture(mediaTexture, ALL_SIDES);
        }
        
        // listen for commands
        llListen(dialogChannel, "", llGetOwner(), "");
    }
    
    touch_start(integer num_detected)
    {
        llDialog(llDetectedKey(0), "What would you like to do?", 
            [changeCommand, pauseCommand, playCommand], dialogChannel);
    }
    
    listen(integer channel, string name, key id, string msg)
    {
        if (msg == changeCommand)
        {
            changeChannel();
        }
        else if (msg == pauseCommand)
        {
            pause();
        }
        else if (msg == playCommand)
        {
            play();
        }
    }

}