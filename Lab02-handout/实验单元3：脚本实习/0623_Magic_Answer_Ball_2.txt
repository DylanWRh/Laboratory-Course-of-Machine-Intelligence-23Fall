// Magic Answer Ball
// Version 2
// 
// Provide a random personalized answer to anyone or anything speaks
// on the public channel

// The global variable holding all the answers
list answers = [
    // positive answers
    "Yes, ",
    "It is certain, ",
    "All signs point to yes, ",
    "You may rely on it, ",
    "Without a doubt, ",
    // negative answers
    "No, ",
    "Absolutely not, ",
    "Doubtful, ",
    "Most assuredly not, ",
    "My sources say no, ",
    //non commital answers
    "Outlook hazy, ",
    "Uncertain, ",
    "The answer is unknowable, ",
    "I'd better not tell you now, ",
    "Cannot predict that at this time, "
];

// pick a random answer from the list of answers and return it
string generateAnswer()  
{
    // generate a random number between 0 and the length of the 
       // list (but not exactly the length of the list), then cast it
       // to an integer. This will give us a random number betwen 0 
       // and the length of the list - 1.
    integer answerIndex = 
        (integer)llFrand(llGetListLength(answers));
    
    // return the answerIndex'th item of the list answers
    return llList2String(answers, answerIndex);
}    

default
{
    state_entry()
    {
        // listen on the public chat channel for any text from 
        // anyone or anything.
        llListen(0, "", NULL_KEY, "");
    }
    
    listen(integer channel, string name, key id, string message)
    {
        // script heard something, answer
        llSay(0, generateAnswer() + name);
    }
}
